pairings -> pairing*
pairing -> header content footer

header -> headline "MO" "TU" "WE" "TH" "FR" "SA" "SU" "DAY" "FLT#" "DEP" "ARR" "DEP" "ARR" "BLK" "TOG" "DUTY" "CREDIT" "LO" "A/C" "CREW" "COMP" "\n"
headline -> ( preliminary-headline | final-headline ) "\n"
preliminary-headline -> base common-headline
final-headline -> trip-id pairing-id common-headline
common-headline -> pairing-type crew base-iata days-of-week effectiveness

base -> "EDMONTON" | "VANCOUVER" | "WINNIPEG" | "CALGARY" | "TORONTO"
base-iata -> ( "YEG" | "YVR" | "YEG" | "YYC" | "YYZ" ) ":" 
base-initial -> ( "E" | "V" | "W" | "C" | "T" )

trip-id -> "TRIP #" \d+
pairing-id -> base-initial ( ["A"-"Z"] | \d )+
pairing-type -> "(" ( "OT" | "OL" | "MT" | "ML" ) ")"

crew -> "[" \d "," \d "," \d "," \d "]"

days-of-week -> day-one day-two day-three day-four day-five day-six day-seven
day-one -> "_" | "1"
day-two -> "_" | "2"
day-three -> "_" | "3"
day-four -> "_" | "4"
day-five -> "_" | "5"
day-six -> "_" | "6"
day-seven -> "_" | "7"

effectiveness -> "effective" period exceptions
period -> month day-to-month day-of-month
month -> "JAN" | "FEB" | "MAR" | "APR" | "MAY" | "JUN" | "JUL" | "AUG" | "SEP" | "OCT" | "NOV" | "DEC"
day-of-month -> \d\d
days-of-month -> ( month day-of-month )+
day-to-month -> day-of-month "-" month
exceptions -> ( "except" days-of-month | "no exceptions.")

contents -> ( content "\n" )+
content -> content-separator | checkin | checkout | leg | layover
content-separator -> calendar-week? "----" "-----------" "---" "---" "-----" "-----" "-----" "-----" "-----" "---------" "-----" "---" "---------"
calendar-week -> ["A"-"Z"]? ( "--" | \d\d ){1,7}

checkin -> calendar-week? report? out tog checkin-duty?
report -> "RPT"
checkin-duty -> "MO" | "RE"

checkout -> calendar-week? release? in block checkout-duty credit
release -> "RLS"
checkout-duty -> duration

leg -> calendar-week? duty-day flight-number origin destination out in block tog? credit aircraft crew

layover -> calendar-week? "----" accomodation "/" transport "-"+ layover-duration "(" transport-duration ")" rest-duration
accomodation -> [^"/"]+
transport -> [^"-"]+

flight-number -> deadhead? \d+
deadhead -> "DH_"
aircraft -> "_73G" | "_7M8" | "_7S8" | "_73H"

origin -> ["A"-"Z"]{3} // IATA 3 letter code
destination -> ["A"-"Z"]{3} // IATA 3 letter code

out -> time
in -> time
block -> duration
tog -> duration | duration "*" | "*" // meaning of * for tog ?
credit -> duration [ credit-annotation ]
credit-annotation -> "(" ( "M" | "L" | "R" | "D" ) ")" // M, L, D, T
layover-duration -> duration
transport-duration -> duration "*2"
rest-duration -> duration

hours -> \d+ "." \d+
time -> \d\d ":" \d\d
duration -> \d "h" \d\d
duty-day -> weekday day-of-duty
weekday -> "Mo" | "Tu" | "We" | "Th" | "Fr" | "Sa" | "Su"
day-of-duty -> \d // positive digit between 1 and 5 (semantics)

footer ->  footer-separator summary footer-separator
footer-separator -> "____________________________________________________________________________________________________"
summary -> tafb-summary credit-summary perdiem-summary
tafb-summary -> "TAFB:" duration
credit-summary -> "Credit" "Time:" credit
perdiem-summary -> "PERDIEM:" hours
